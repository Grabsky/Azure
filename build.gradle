plugins {
    id "java-library"
}

group = "cloud.grabsky"
version = "1.21-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

subprojects {
    apply(plugin: "java-library")

    group = rootProject.group
    version = rootProject.version

    ext {
        // Environment-specific variables...
        WORKSPACE_BUILDS = System.getenv("WORKSPACE_BUILDS")
        WORKSPACE_PLUGINS = System.getenv("WORKSPACE_PLUGINS")
        // Dependency versions...
        BEDROCK_VERSION = "1.20.1-47"
        KYORI_NBT_VERSION = "4.15.0"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        // Repository for the Paper API.
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        // Repository containing the 'bedrock' dependency.
        maven { url = "https://maven.pkg.github.com/grabsky/bedrock"
            credentials {
                username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        // Repository containing the 'configuration' dependency.
        maven { url = "https://maven.pkg.github.com/grabsky/configuration"
            credentials {
                username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        // Repository containing the 'commands' dependency.
        maven { url = "https://maven.pkg.github.com/grabsky/commands"
            credentials {
                username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    dependencies {
        compileOnly("org.jetbrains:annotations:24.0.1")
        // Lombok
        compileOnly("org.projectlombok:lombok:1.18.32")
        annotationProcessor("org.projectlombok:lombok:1.18.32")
        // Paper API
        compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    }

    compileJava {
        options.fork = true
        // Setting compatibility to Java 17 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
    }

}

tasks.register("release") {
    print("${version.toString().split('-')[0]} #${System.getenv("GITHUB_RUN_NUMBER")}")
}

tasks.register("tag") {
    print(version)
}
