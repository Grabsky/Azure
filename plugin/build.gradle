plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

dependencies {
    api project(":azure-api")
    // Libraries
    implementation("cloud.grabsky:commands:1.20.1-34")
    implementation("cloud.grabsky:configuration-paper:1.20.1-42")
    // Other
    compileOnly("net.luckperms:api:5.4")
}

tasks {
    shadowJar { archiveFileName = "${rootProject.name}-${project.version}.jar" }
    processResources {
        filesMatching("paper-plugin.yml") {
            expand(name: rootProject.name, version: rootProject.version)
        }
    }
}

tasks.register("deploy") {
    dependsOn(shadowJar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(shadowJar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(shadowJar) into(pluginsDirectory) }
    }
}



