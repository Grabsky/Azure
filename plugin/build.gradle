plugins {
    // https://github.com/GradleUp/shadow
    id "com.gradleup.shadow" version "9.0.0"
    // https://github.com/jpenilla/run-task
    id "xyz.jpenilla.run-paper" version "2.3.1"
    // https://github.com/eldoriarpg/plugin-yml
    id "de.eldoria.plugin-yml.paper" version "0.7.1"
}

repositories {
    // Repository containing the 'PlaceholderAPI' dependency.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing the 'AuroraQuests' dependency.
    maven { url = "https://repo.auroramc.gg/releases/" }
    // Repository containing the 'ExcellentShop' dependency.
    maven { url = "https://repo.nightexpressdev.com/releases/" }
}

dependencies {
    api(project(":api"))
    // https://github.com/Grabsky/bedrock
    paperLibrary("cloud.grabsky:bedrock:$BEDROCK_VERSION")
    // https://github.com/Grabsky/commands
    paperLibrary("cloud.grabsky:commands:1.20.1-40")
    // https://github.com/Grabsky/configuration
    paperLibrary("cloud.grabsky:configuration-paper:1.21.4-54")
    // https://github.com/KyoriPowered/adventure
    paperLibrary("net.kyori:adventure-nbt:$KYORI_NBT_VERSION")
    // https://github.com/discord-jda/JDA
    paperLibrary("net.dv8tion:JDA:6.0.0-rc.2") {
        exclude module: "opus-java"
    }
    // https://github.com/MinnDevelopment/discord-webhooks
    paperLibrary("club.minnced:discord-webhooks:0.8.4")
    // https://github.com/felldo/JEmoji
    paperLibrary("net.fellbaum:jemoji:1.7.4")
    // https://github.com/mfnalex/MorePersistentDataTypes
    paperLibrary("com.jeff-media:MorePersistentDataTypes:2.4.0")
    // https://github.com/LuckPerms/LuckPerms
    compileOnly("net.luckperms:api:5.5")
    // https://github.com/PlaceholderAPI/PlaceholderAPI
    compileOnly("me.clip:placeholderapi:2.11.6")
    // https://github.com/AuroraNetworkStudios/Aurora
    compileOnly("gg.auroramc:Aurora:2.3.4")
    // https://github.com/AuroraNetworkStudios/AuroraQuests
    compileOnly("gg.auroramc:AuroraQuests:2.0.1")
    // https://github.com/nulli0n/ExcellentShop-spigot
    compileOnly("su.nightexpress.excellentshop:Core:4.15.0")
}

tasks {
    shadowJar {
        // Changing name of the artifact.
        archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    runServer {
        minecraftVersion("1.21.8")
        jvmArgs("--enable-preview")
    }
}

paper {
    name = "Azure"
    main = "cloud.grabsky.azure.Azure"
    loader = "cloud.grabsky.azure.Azure\$PluginLoader"
    apiVersion = "1.21.8"
    generateLibrariesJson = true
    serverDependencies {
        "LuckPerms"      { load = "BEFORE"; required = true }
        "PlaceholderAPI" { load = "BEFORE"; required = true }
        "AuroraQuests"   { load = "BEFORE"; required = false }
        "ExcellentShop"  { load = "BEFORE"; required = false }
    }
}


