plugins {
    id "de.eldoria.plugin-yml.paper" version "0.7.1"
    id "com.gradleup.shadow" version "8.3.6"
}

repositories {
    // Repository containing the 'PlaceholderAPI' dependency.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing the 'AuroraQuests' dependency.
    maven { url = "https://repo.auroramc.gg/releases/" }
    // Repository containing the 'ExcellentShop' dependency.
    maven { url = "https://repo.nightexpressdev.com/releases/" }
}

dependencies {
    api(project(":azure-api"))
    // Must share version between 'plugin' and 'api' modules.
    implementation("cloud.grabsky:bedrock:$BEDROCK_VERSION")
    implementation("cloud.grabsky:commands:1.20.1-38")
    implementation("cloud.grabsky:configuration-paper:1.21.4-47")
    // Must share version between 'plugin' and 'api' modules.
    implementation("net.kyori:adventure-nbt:$KYORI_NBT_VERSION")
    // Moshi and Okio are already in the classpath.
    implementation("org.javacord:javacord:3.8.0") {
        exclude group: "com.squareup.moshi"
        exclude group: "com.squareup.okio"
    }
    // JEmoji (Shaded)
    implementation("net.fellbaum:jemoji:1.7.1")
    // LuckPerms API (Provided)
    compileOnly("net.luckperms:api:5.4")
    // PlaceholderAPI (Provided)
    compileOnly("me.clip:placeholderapi:2.11.6")
    // Aurora and AuroraQuests
    compileOnly("gg.auroramc:Aurora:2.1.6")
    compileOnly("gg.auroramc:AuroraQuests:1.3.16")
    // ExcellentShop (Provided)
    compileOnly("su.nightexpress.excellentshop:Core:4.15.0")
    // MorePersistentDataTypes
    implementation("com.jeff-media:MorePersistentDataTypes:2.4.0")
}

tasks {
    shadowJar {
        // Changing name of the artifact.
        archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
}

paper {
    name = "Azure"
    main = "cloud.grabsky.azure.Azure"
    apiVersion = "1.21.4"
    serverDependencies {
        // Required
        "LuckPerms"      { load = "BEFORE"; required = true }
        "PlaceholderAPI" { load = "BEFORE"; required = true }
        // Optional
        "AuroraQuests"   { load = "BEFORE"; required = false }
        "ExcellentShop"  { load = "BEFORE"; required = false }
    }
}


